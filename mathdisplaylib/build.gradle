apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'maven-publish'

group='com.github.gregcockroft'

android {
    compileSdkVersion 32



    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 32
        versionCode 1
        versionName "1.2"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        sourceSets.test.resources.srcDirs += ["src/main"]
        multiDexEnabled true
        externalNativeBuild {
            cmake {
                arguments "-Dcdep-dependencies_DIR=../../../.cdep/modules"
                arguments "-DANDROID_STL=c++_shared"
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        main {
            // let gradle pack the shared library into apk
            jniLibs.srcDirs = ['../distribution/gperf/lib']
        }
    }

    externalNativeBuild {
        cmake {
            path 'src/main/cpp/CMakeLists.txt'
        }
    }
    ndkVersion "23.1.7779620"
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'junit:junit:4.12'
    implementation 'com.android.support:support-annotations:28.0.0'
    implementation 'com.android.support.test:runner:1.0.2'
    implementation 'com.android.support.test:rules:1.0.2'
    api "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'com.github.jitpack:gradle-simple:1.0'
}
repositories {
    mavenCentral()
    google()
    jcenter()
    maven { url "https://jitpack.io" }
}

// This pulls in freetype. Dependencies are in cdep.yml
// This only needs to run once but is being run before every build right now so that gradle command line builds will work

task runcdep(type:Exec) {
    commandLine './cdep'
}

preBuild.dependsOn(runcdep)


// For Android Studio this needs to be run once from the command line
//
//  cd mathdisplaylib
//  ./cdep
afterEvaluate {
    publishing {
        publications {
            // Creates a Maven publication called "release".
            release(MavenPublication) {
                from components.release
                groupId = 'com.github.gregcockroft'
                artifactId = 'AndroidMath'
                version = 'v1.0.0'
            }
        }
    }
}
